plugins {
    id 'java-library'
    id 'maven-publish'
    id "jacoco"
    id "org.sonarqube" version "5.1.0.4882"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9
}

sourceSets {
    common {
        java {
            srcDir 'src/main/java'
        }
    }
    v1 {
        java {
            srcDir 'src/main/java-v1'
        }
        compileClasspath += sourceSets.common.output
        compileClasspath += configurations.compileClasspath
    }
    v2 {
        java {
            srcDir 'src/main/java-v2'
        }
        compileClasspath += sourceSets.common.output
        compileClasspath += configurations.compileClasspath
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:-options"]
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    commonCompileOnly 'org.jetbrains:annotations:26.0.1'

    v1CompileOnly 'org.yaml:snakeyaml:1.14'
    v2CompileOnly 'org.yaml:snakeyaml:2.2'

    testImplementation sourceSets.common.output
    testImplementation sourceSets.v1.output
    testImplementation sourceSets.v2.output

    testRuntimeOnly 'org.yaml:snakeyaml:1.14'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.jetbrains:annotations:26.0.1'
}


tasks.register('jarBoth', Jar) {
    from sourceSets.common.output
    from sourceSets.v1.output
    from sourceSets.v2.output
    archiveClassifier = 'both'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.name
            version = project.version
        }
    }
    repositories {
        mavenLocal()
    }
}

sonar {
    properties {
        property "sonar.projectKey", "AvarionMC_yaml"
        property "sonar.organization", "avarionmc"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}