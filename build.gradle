plugins {
    id 'java-library'
    id 'maven-publish'
    id "jacoco"
    id "org.sonarqube" version "5.1.0.4882"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9
}

sourceSets {
    main {
        java.srcDirs = []
        resources.srcDirs = []
    }
    common {
        java {
            srcDir 'src/main/java'
        }
    }
    v1 {
        java {
            srcDir 'src/main/java-v1'
        }
        compileClasspath += sourceSets.common.output
    }
    v2 {
        java {
            srcDir 'src/main/java-v2'
        }
        compileClasspath += sourceSets.common.output
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        compileClasspath = configurations.testCompileClasspath
        compileClasspath += sourceSets.common.output
        compileClasspath += sourceSets.v1.output
        compileClasspath += sourceSets.v2.output
        runtimeClasspath = configurations.testRuntimeClasspath
        runtimeClasspath += sourceSets.common.output
        runtimeClasspath += sourceSets.v1.output
        runtimeClasspath += sourceSets.v2.output
    }
}

configurations {
    commonImplementation
    v1Implementation.extendsFrom commonImplementation
    v2Implementation.extendsFrom commonImplementation
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:-options"]
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    commonImplementation 'org.jetbrains:annotations:26.0.1'

    v1Implementation 'org.yaml:snakeyaml:1.14'
    v2Implementation 'org.yaml:snakeyaml:2.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jar {
    from sourceSets.common.output
    from sourceSets.v1.output
    from sourceSets.v2.output
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('testV1', Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.test.output.classesDirs
    def testV1Config = configurations.create("testV1Runtime")
    classpath = configurations.testRuntimeClasspath +
            sourceSets.test.output +
            sourceSets.common.output +
            sourceSets.v1.output +
            sourceSets.v2.output +
            testV1Config
    configurations {
        testV1Runtime
    }
    dependencies {
        testV1Runtime 'org.yaml:snakeyaml:1.14'
    }
}
tasks.register('testV2', Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = configurations.testRuntimeClasspath +
            sourceSets.test.output +
            sourceSets.common.output +
            sourceSets.v1.output +
            sourceSets.v2.output
    dependencies {
        testRuntimeOnly 'org.yaml:snakeyaml:2.2'
    }
}

jacocoTestReport {
    dependsOn test
    executionData.setFrom(fileTree(project.buildDir).include("jacoco/*.exec"))

    sourceDirectories.setFrom files(
            sourceSets.common.allJava.srcDirs,
            sourceSets.v1.allJava.srcDirs,
            sourceSets.v2.allJava.srcDirs
    )
    classDirectories.setFrom files(
            sourceSets.common.output,
            sourceSets.v1.output,
            sourceSets.v2.output
    )
}

test {
    useJUnitPlatform()

    doFirst {
        // Clean existing exec data
        delete fileTree(dir: project.buildDir, includes: ['jacoco/*.exec'])
    }

    dependsOn testV1, testV2

    // Skip actual test execution since we ran them in v1/v2
    enabled = false

    finalizedBy jacocoTestReport
}

sonar {
    properties {
        property "sonar.projectKey", "AvarionMC_yaml"
        property "sonar.organization", "avarionmc"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
